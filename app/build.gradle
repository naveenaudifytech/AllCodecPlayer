plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    ndkVersion '21.4.7075529'

    defaultConfig {
        applicationId "com.allcodecaudify.player"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 45
        versionName "0.45"
        archivesBaseName = "Allcodec.Audify.Player.v${versionName}"
    }

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "exoplayer"
    productFlavors {
        exoMaven {
            dimension "exoplayer"
        }
        exoLocal {
            dimension "exoplayer"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    def exoplayerVersion = '2.14.2'
    // Use patched exoplayer-core - map Dolby Vision profile 7 to HEVC
    // https://github.com/google/ExoPlayer/issues/9139
    exoMavenImplementation "com.google.android.exoplayer:exoplayer-core:$exoplayerVersion"
    exoLocalImplementation "com.google.android.exoplayer:exoplayer-common:$exoplayerVersion"
    exoLocalImplementation "com.google.android.exoplayer:exoplayer-extractor:$exoplayerVersion"
    exoMavenImplementation "com.google.android.exoplayer:exoplayer-ui:$exoplayerVersion"
    exoLocalImplementation("com.google.android.exoplayer:exoplayer-ui:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation "com.google.android.exoplayer:extension-mediasession:$exoplayerVersion"
    exoMavenImplementation "com.google.android.exoplayer:exoplayer-dash:$exoplayerVersion"
    exoLocalImplementation("com.google.android.exoplayer:exoplayer-dash:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    exoMavenImplementation "com.google.android.exoplayer:exoplayer-hls:$exoplayerVersion"
    exoLocalImplementation("com.google.android.exoplayer:exoplayer-hls:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    exoMavenImplementation "com.google.android.exoplayer:exoplayer-smoothstreaming:$exoplayerVersion"
    exoLocalImplementation("com.google.android.exoplayer:exoplayer-smoothstreaming:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    exoMavenImplementation "com.google.android.exoplayer:exoplayer-rtsp:$exoplayerVersion"
    exoLocalImplementation("com.google.android.exoplayer:exoplayer-rtsp:$exoplayerVersion") {
        exclude group: "com.google.android.exoplayer", module: "exoplayer-core"
    }
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.13.3'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'com.ibm.icu:icu4j:69.1'
    implementation 'com.arthenica:ffmpeg-kit-min:4.4.LTS'
    implementation project(path: ':doubletapplayerview')
    implementation project(path: ':android-file-chooser')
    implementation fileTree(dir: "libs", include: ["extension-*.aar"])
    exoLocalImplementation fileTree(dir: "libs", include: ["library-*.aar"])
}